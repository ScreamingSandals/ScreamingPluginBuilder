import com.github.jengelman.gradle.plugins.shadow.ShadowExtension

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

defaultTasks 'clean', 'build', 'publishToMavenLocal'

if (project.hasProperty('nexus')) {
    defaultTasks.add('publish')
}


repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url 'https://maven.fabricmc.net/' }
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    implementation 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
    implementation 'kr.entree:spigradle:2.2.3'
    implementation 'io.franzbecker:gradle-lombok:4.0.0'
    implementation 'net.fabricmc:fabric-loom:0.4-SNAPSHOT'
}

group = 'org.screamingsandals.gradle.builder'
version = '1.0.25'

publishing {
    publications {
        maven(MavenPublication) {
            ShadowExtension shadow = project.extensions.getByName("shadow")
            shadow.component(it)

            it.artifacts.every {
                it.classifier = ""
            }

            pom.withXml {
                if (asNode().get("dependencies") != null) {
                    asNode().get("dependencies").each {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }

    if (project.hasProperty("nexus")) {
        repositories {
            maven {
                credentials {
                    username = System.getenv("NEXUS_USERNAME")
                    password = System.getenv("NEXUS_PASSWORD")
                }

                name = 'nexus'

                if (((String) project.version).contains("SNAPSHOT")) {
                    url = System.getenv("NEXUS_URL_SNAPSHOT")
                } else {
                    url = System.getenv("NEXUS_URL_RELEASE")
                }
            }
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
