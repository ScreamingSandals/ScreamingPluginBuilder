plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

defaultTasks 'clean', 'build'

def ciCdOptimized = System.getenv("OPTIMIZE_FOR_CI_CD") == "1"

if (!ciCdOptimized) {
    defaultTasks.add('publishToMavenLocal')
}

if (System.getenv("GITLAB_REPO") != null || (System.getenv("NEXUS_URL_SNAPSHOT") != null && System.getenv("NEXUS_URL_RELEASE") != null)) {
    defaultTasks.add('publish')
}


repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    implementation 'gradle.plugin.com.github.johnrengelman:shadow:' + SHADOW_PLUGIN
    implementation 'io.franzbecker:gradle-lombok:' + LOMBOK_PLUGIN //switch to freefair, this one is old
    implementation 'com.jcraft:jsch:' + JSCH_PLUGIN
    implementation 'com.google.code.gson:gson:' + GSON_PLUGIN
}

group = 'org.screamingsandals.gradle.builder'
version = PLUGIN_VERSION

gradlePlugin {
    plugins {
        pluginBuilder {
            id = 'org.screamingsandals.plugin-builder'
            implementationClass = 'org.screamingsandals.gradle.builder.BuilderPlugin'
        }
        pluginBuilderLite {
            id = 'org.screamingsandals.plugin-builder-lite'
            implementationClass = 'org.screamingsandals.gradle.builder.LiteBuilderPlugin'
        }
    }
}

publishing {
    if (System.getenv("GITLAB_REPO") != null) {
        repositories {
            maven {
                url System.getenv("GITLAB_REPO")
                name "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = System.getenv("GITLAB_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }

    if (System.getenv("NEXUS_URL_SNAPSHOT") != null && System.getenv("NEXUS_URL_RELEASE") != null) {
        repositories {
            maven {
                if (((String) project.version).contains("SNAPSHOT")) {
                    it.url = System.getenv("NEXUS_URL_SNAPSHOT")
                } else {
                    it.url = System.getenv("NEXUS_URL_RELEASE")
                }
                it.credentials.username = System.getenv("NEXUS_USERNAME")
                it.credentials.password = System.getenv("NEXUS_PASSWORD")
            }
        }
    }
}

sourceCompatibility = 11
