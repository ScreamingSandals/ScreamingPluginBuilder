import com.github.jengelman.gradle.plugins.shadow.ShadowExtension

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

defaultTasks 'clean', 'build', 'publishToMavenLocal'

if (System.getenv("GITLAB_REPO") != null) {
    defaultTasks.add('publish')
}


repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    implementation 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
    implementation 'kr.entree:spigradle:2.2.3' // for removal
    implementation 'io.franzbecker:gradle-lombok:4.0.0'
}

group = 'org.screamingsandals.gradle.builder'
version = '1.0.34'

publishing {
    publications {
        maven(MavenPublication) {
            ShadowExtension shadow = project.extensions.getByName("shadow")
            shadow.component(it)

            it.artifacts.every {
                it.classifier = ""
            }

            pom.withXml {
                if (asNode().get("dependencies") != null) {
                    asNode().get("dependencies").each {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }

    if (System.getenv("GITLAB_REPO") != null) {
        repositories {
            maven {
                url System.getenv("GITLAB_REPO")
                name "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = System.getenv("GITLAB_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
}

sourceCompatibility = 11
