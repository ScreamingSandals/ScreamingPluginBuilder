defaultTasks 'clean', 'build'

def ciCdOptimized = System.getenv("OPTIMIZE_FOR_CI_CD") == "1"

if (!ciCdOptimized) {
    defaultTasks.add('publishToMavenLocal')
}

if (System.getenv("GITLAB_REPO") != null || (System.getenv("NEXUS_URL_SNAPSHOT") != null && System.getenv("NEXUS_URL_RELEASE") != null)) {
    defaultTasks.add('publish')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-gradle-plugin'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        compileOnly gradleApi()
        compileOnly libs.projectlombok
        compileOnly libs.jetbrains.annotations
        annotationProcessor libs.projectlombok

        implementation libs.shadow
    }

    publishing {
        if (System.getenv("GITLAB_REPO") != null) {
            repositories {
                maven {
                    url System.getenv("GITLAB_REPO")
                    name "GitLab"
                    credentials(HttpHeaderCredentials) {
                        name = 'Private-Token'
                        value = System.getenv("GITLAB_TOKEN")
                    }
                    authentication {
                        header(HttpHeaderAuthentication)
                    }
                }
            }
        }

        if (System.getenv("NEXUS_URL_SNAPSHOT") != null && System.getenv("NEXUS_URL_RELEASE") != null) {
            repositories {
                maven {
                    if (((String) project.version).contains("SNAPSHOT")) {
                        it.url = System.getenv("NEXUS_URL_SNAPSHOT")
                    } else {
                        it.url = System.getenv("NEXUS_URL_RELEASE")
                    }
                    it.credentials.username = System.getenv("NEXUS_USERNAME")
                    it.credentials.password = System.getenv("NEXUS_PASSWORD")
                }
            }
        }
    }

    sourceCompatibility = 11
}

